Option Compare Database
Option Explicit

Public Const Ruta As String = "W:\Educación\DistanciaAGL\Proyecto\DAMProyecto\MicrosoftAccess"

'Exportar todos los objetos Access para poder hacer versionado con GIT
Public Sub ExportarObjetosAccess()

    Dim obj As AccessObject
    
    If Dir(Ruta, vbDirectory) = "" Then
        MkDir Ruta
    End If
    
    ' Exportar formularios
    For Each obj In CurrentProject.AllForms
        Debug.Print "Exportando formulario: " & obj.Name
        SaveAsText acForm, obj.Name, Ruta & obj.Name & ".txt"
    Next obj
    
    ' Exportar informes
    For Each obj In CurrentProject.AllReports
        Debug.Print "Exportando informe: " & obj.Name
        SaveAsText acReport, obj.Name, Ruta & obj.Name & ".txt"
    Next obj
    
    ' Exportar módulos
    For Each obj In CurrentProject.AllModules
        Debug.Print "Exportando informe: " & obj.Name
        SaveAsText acModule, obj.Name, Ruta & obj.Name & ".txt"
    Next obj
    
    MsgBox "Exportación completada", vbInformation

End Sub

'Importar todos los objetos Access para poder hacer versionado con GIT

Sub ImportarObjetosAccess()
    On Error GoTo Err_Global
    
    Dim strArchivo As String, strNombre As String
    Dim intContadorOK As Integer: intContadorOK = 0
    Dim intContadorError As Integer: intContadorError = 0
    Dim strErrores As String
    
        ' Validar si la carpeta existe
    If Dir(Ruta, vbDirectory) = "" Then
        MsgBox "La ruta especificada no existe.", vbCritical
        Exit Sub
    End If

    strArchivo = Dir(Ruta & "*.txt")

    Do While strArchivo <> ""
        strNombre = Replace(strArchivo, ".txt", "")
        
        ' Usamos un manejador de errores local para no detener el bucle
        On Error Resume Next
        
        If Left(strNombre, 4) = "frm_" Then
            Application.LoadFromText acForm, strNombre, Ruta & strArchivo
        ElseIf Left(strNombre, 4) = "inf_" Then
            Application.LoadFromText acReport, strNombre, Ruta & strArchivo
        ElseIf Left(strNombre, 4) = "mod_" Then
            Application.LoadFromText acModule, strNombre, Ruta & strArchivo
        Else
            ' Caso opcional: archivos que no cumplen el patrón
            GoTo SiguienteArchivo
        End If

        ' Verificar si hubo error en la carga de este archivo específico
        If Err.Number <> 0 Then
            intContadorError = intContadorError + 1
            strErrores = strErrores & "- " & strArchivo & " (Error: " & Err.Description & ")" & vbCrLf
            Err.Clear
        Else
            intContadorOK = intContadorOK + 1
        End If
        
        On Error GoTo Err_Global ' Volver al control general
        
SiguienteArchivo:
        strArchivo = Dir()
    Loop

    ' Informe final
    MsgBox "Proceso finalizado." & vbCrLf & _
           "Exitosos: " & intContadorOK & vbCrLf & _
           "Errores: " & intContadorError, vbInformation
           
    If strErrores <> "" Then
        Debug.Print "DETALLE DE ERRORES:" & vbCrLf & strErrores
        MsgBox "Hubo errores en algunos archivos. Revisa la ventana Inmediato (Ctrl+G) para ver los detalles.", vbInformation
    End If

    Exit Sub

Err_Global:
    MsgBox "Error crítico inesperado: " & Err.Description, vbCritical
End Sub